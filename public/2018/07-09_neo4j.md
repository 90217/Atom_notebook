# ___2018 - 07 - 09 Neo4j___
***

# 目录
***

# Neo4j
## 安装
  - [neo4j github](https://github.com/neo4j/neo4j)
  - [Neo4j Graphs for Everyone](https://neo4j.com)
  - [Neo4j Desktop Debian Installation](https://neo4j.com/docs/operations-manual/current/installation/linux/debian/)
  - [Neo4j Debian Packages](https://debian.neo4j.org)
  - [Other Neo4j Releases](https://debian.neo4j.org)
  - [O’Reilly’s Graph Databases](https://neo4j.com/graph-databases-book/?ref=home)
  - **安装 Neo4j**
    ```shell
    ''' Neo4j 要求 JAVA 1.8 '''
    apt install maven openjdk-8-jdk
    apt install debhelper devscripts dos2unix dpkg make xmlstarlet
    export JAVA_HOME=/usr/lib/jvm/java-1.8.0-openjdk-amd64

    ''' 方法1，Github 中的安装方法，启动一个安装程序，需要更多配置 '''
    # You will need a license for install4j, which is only needed for Neo4j Desktop
    curl -O http://download-keycdn.ej-technologies.com/install4j/install4j_linux_6_1_4.deb
    dpkg -i install4j_linux_6_1_4.deb

    ''' 方法2，debian source list，E: The repository 'https://debian.neo4j.org/repo stable/ Release' does not have a Release file. '''
    wget -O - https://debian.neo4j.org/neotechnology.gpg.key | sudo apt-key add -
    echo 'deb https://debian.neo4j.org/repo stable/' | sudo tee -a /etc/apt/sources.list.d/neo4j.list
    sudo apt-get update

    ''' 方法3，tar 文件 '''
    # https://neo4j.com/download/other-releases/#releases
    wget https://go.neo4j.com/download-thanks.html?edition=community&release=3.4.1&flavour=unix
    tar xvf neo4j-community-3.4.1-unix.tar.gz
    cd neo4j-community-3.4.1
    cd bin
    ./neo4j console
    # INFO  Remote interface available at http://localhost:7474/

    # Add bin to envirenment PATH
    ```
  - **启动**
    ```shell
    neo4j start
    # Browse at http://localhost:7474/
    Default username / password: neo4j / neo4j
    ```
  - **Hello world**
    ```python
    CREATE (matrix:Movie { title:"The Matrix", released:1997 })
    CREATE (cloudAtlas:Movie { title:"Cloud Atlas", released:2012 })
    CREATE (forrestGump:Movie { title:"Forrest Gump", released:1994 })
    CREATE (keanu:Person { name:"Keanu Reeves", born:1964 })
    CREATE (robert:Person { name:"Robert Zemeckis", born:1951 })
    CREATE (tom:Person { name:"Tom Hanks", born:1956 })
    CREATE (tom)-[:ACTED_IN { roles: ["Forrest"]}]->(forrestGump)
    CREATE (tom)-[:ACTED_IN { roles: ['Zachry']}]->(cloudAtlas)
    CREATE (robert)-[:DIRECTED]->(forrestGump)
    RETURN matrix,cloudAtlas,forrestGump,keanu,robert,tom
    ```
    ![](images/neo4j_hello_world.png)
## Basic concept
  - **Graph 图** 是表示对象与对象之间关系的方法
    - **对象** 又称 节点 Node / 顶点 Vertex / 实体 Entity，描述具体的一件事物
    - **关系** 又称 边 edge，描述对象之间的关系
    - **G=(V, E)** 表示一张图，V 表示节点的集合，E 表示边集合

    ![](images/neo4j_node_example.png)     
  - **图的学习任务分类**
    - 社交网络 / 搜索排名 / 评分系统 / 推荐系统 / 知识图谱 / 风险控制
    - 节点上的任务：利用图中节点的关系可以做包括分类（比如风险识别、价值排名等任务）、聚类（社群检测、用户画像等任务）
    - 边上的任务：通过图中节点之间是否有边可以做比如推荐、知识推理等任务
    - 图上的任务：图可以做像高分子分类、3D视觉分类等任务
  - **Graph 图** 基本组成
    - **Nodes 节点** 数据的名称
      ```js
      (a) //actors
      (m) //movies
      ( ) //some anonymous nod
      ```
    - **Relationships 关系** 节点之间的连接
      ```js
      -[r]-> //a relationship referred to as "r"
      (a)-[r]->(m) //actors having a relationship referred to as "r" to movies
      -[:ACTED_IN]-> //the relationship type is ACTED_IN
      (a)-[:ACTED_IN]->(m) //actors that ACTED_IN some movie
      (d)-[:DIRECTED]->(m) //directors that DIRECTED some movie
      ```
      - 关系有特定的方向与类型
      - 关系可以包含属性
    - **Properties 属性** 键值队的形式存储数据值
      ```js
      (m {title:"The Matrix"}) //Movie with a title property
      (a {name:"Keanu Reeves",born:1964}) //Actor with name and born property
      (a)-[:ACTED_IN {roles:["Neo"]}]->(m) //Relationship ACTED_IN with roles property
      ```
    - **Label 标签** 节点可以通过标签组合在一起
      ```js
      (a:Person) //a Person
      (a:Person {name:"Keanu Reeves"}) //a Person with properties
      (a:Person)-[:ACTED_IN]->(m:Movie) //a Person that ACTED_IN some movie
      ```
      - 一个节点可以有 0 个或多个标签
      - 标签没有属性
  - **Editor**
    - **:help** 查看命令的帮助信息
    - **:clear** 清空
    - **:play** 运行
      ```js
      // Guides
      :play intro
      :play concepts
      :play cypher

      // Examples
      :play movie graph
      :play northwind graph
      ```
    - **<shift-enter>** Switch to multi-line editing
    - **<ctrl-enter>** 运行
## Cypher Basic
  - **Cypher** Neo4j 图查询 / 创建语言，描述 **找什么**，而不是 **怎么找**
  - **CREATE** 创建节点
    ```js
    // 语法规则
    CREATE (节点名: 标签 {节点属性})
    ```
    ```js
    CREATE (ee:Person { name: "Emil", from: "Sweden", klout: 99 })
    ```
  - **DELETE** 删除当前所有节点 / 关系
    ```js
    MATCH(n:Person) DETACH
    DELETE n
    ```
  - **MATCH** 查找节点或关系
    ```js
    // 语法规则
    MATCH (匹配出的变量名 : 匹配的标签) WHERE 过滤结果 RETURN 返回特定结果
    ```
    ```js
    MATCH (pp:Person) WHERE pp.name = "Emil" RETURN pp;
    ```
    CREATE 后面直接用 MATCH 时，需要 WITH
    ```js
    create (ee:Person {name: "Emil", from: "Sweden", klout: 99})
    with ee
    match (pp:Person) where pp.name="Emil" return pp
    ```
    ![](images/neo4j_match.png)
  - **CREATE relationships** 创建关系
    ```js
    // 语法规则
    CREATE (节点名 1)-[:关系名 {关系属性}]->(节点名 2)
    ```
    ```js
    create (ee:Person {name: "Emil", from: "Sweden", klout: 99})
    create (et:Person {name: "et"})
    create (et)-[:KNOWS {by: "swimming"}]->(ee)
    return ee, et
    ```
    ![](images/neo4j_relation.png)
  - 创建多个节点以及关系
    ```js
    create (ee:Person {name: "Emil", from: "Sweden", klout: 99})
    WITH ee
    MATCH (pp:Person) WHERE pp.name="Emil"
    CREATE (js:Person {name: "Johan", from: "Sweden", learn: "surfing"}),
           (ir: Person {name: "Ian", from: "Belgium", title: "author"}),
           (rvb: Person {name: "Rik", from: "England", pet: "Orval"}),
           (ally: Person {name: "Allison", from: "California", hobby: "surfing"}),
           (ee)-[:KNOWS {since: 2001}]->(js), (ee)-[:KNOWS {rating: 5}]->(ir),
           (js)-[:KNOWS]->(ir), (js)-[:KNOWS]->(rvb),
           (ir)-[:KNOWS]->(js), (ir)-[:KNOWS]->(ally),
           (rvb)-[:KNOWS]->(ally)
    RETURN ee, js, ir, rvb, ally
    ```
    ![](images/neo4j_create_relations.png)
  - **MATCH relationships** 匹配关系
    ```js
    // 语法规则
    MATCH (匹配出的变量名 : 匹配的标签) -[:关系]-(匹配到的关系名)
    WHERE 过滤结果 RETURN 返回特定结果
    ```
    ```js
    // pattern can be used to find Emil's friends
    MATCH (ee:Person)-[:KNOWS]-(friends)
    WHERE ee.name = "Emil" RETURN ee, friends
    ```
    ![](images/neo4j_match_pattern_knows.png)
  - **Recommendations** 匹配关系进行推荐
    ```js
    // Recommd frind by "surfing"
    MATCH (js:Person)-[:KNOWS]-()-[:KNOWS]-(surfer)
    WHERE js.name = "Johan" AND surfer.hobby = "surfing"
    RETURN DISTINCT surfer
    ```
    ![](images/neo4j_recommdation.png)
    - **() empty** 表示忽略这些匹配到的节点
    - **DISTINCT** because more than one path will match the pattern
  - **EXPLAIN / PROFILE** 显示匹配过程
    ```js
    PROFILE MATCH (js:Person)-[:KNOWS]-()-[:KNOWS]-(surfer)
    WHERE js.name = "Johan" AND surfer.hobby = "surfing"
    RETURN DISTINCT surfer
    ```
    ![](images/neo4j_explain.png)
## MATCH CQL查询语句
  运行环境为在线浏览器-数据库是官方演员-电影图数据库

  1
  ```js
  MATCH (n) RETURN n // 查询所有节点
  MATCH (n)-->(m) RETURN n, m // 查询两个节点，一条边
  MATCH (n)-->( ) RETURN n // 也可缺失一个节点
  MATCH (person)-->() RETURN person.name // 返回节点的属性值：返回演员和导演的名字
  ```
  2查询关系-边
  ```js
  MATCH (person)-[rel]->(movie) RETURN person, type(rel) // 查询所有的边关系
  MATCH (actor)-[role:ACTED_IN]->(movie) RETURN actor.name, role.roles, movie.title // 查询哪个演员演了哪个电影：
  ```
  3 查询标签：
  ```js
  MATCH (actor:Person)-[:ACTED_IN]->(movie) RETURN actor.name, movie.title // 用标签查询 哪个演员表演过哪个电影
  MATCH (tom:Person) WHERE tom.name = "Tom Hanks" RETURN tom // 增加WHERE 条件筛选，名字叫Tom Hanks的演员
  MATCH (tom:Person {name:"Tom Hanks"}) RETURN tom // 还可以这样
  ```
  4 path：传递关系
  ```js
  // 查询哪个演员参演的电影，及这部电影的导演关系图
  MATCH (actor)-[:ACTED_IN]->(movie)<-[:DIRECTED]-(director)
  RETURN actor.name, movie.title, director.name;

  // 上面语句的另一种写法
  MATCH (actor)-[:ACTED_IN]->(movie),
        (movie)<-[:DIRECTED]-(director)
  RETURN actor.name, movie.title, director.name

  // 再来一种等价写法
  MATCH (actor)-[:ACTED_IN]->(movie),
        (director)-[:DIRECTED]->(movie)
  RETURN actor.name, movie.title, director.name;
  ```
  定义path
  ```js
  // 用p返回多有的path及其属性
  MATCH p=(a)-[:ACTED_IN]->(m)<-[:DIRECTED]-(d)
  RETURN p

  // 用节点node返回path
  MATCH p=(a)-[:ACTED_IN]->(m)<-[:DIRECTED]-(d)
  RETURN nodes(p)

  // 用关系rels返回path
  MATCH p=(a)-[:ACTED_IN]->(m)<-[:DIRECTED]-(d)
  RETURN rels(p)

  // 两个模式的path定义
  MATCH p1=(a)-[:ACTED_IN]->(m), p2=(d)-[:DIRECTED]->(m)
  RETURN p1, p2
  ```
***

# Movie Graph
## Create 创建
  - **Movie Graph** 演员-电影 图数据库，演员参演电影的关系图
    ```js
    :play movie-graph
    ```
  - [neo4j_movie_graph.cypher](neo4j_movie_graph.cypher)
    ```js
    // 示例
    CREATE (YouveGotMail:Movie {title:"You've Got Mail", released:1998, tagline:'At odds in life... in love on-line.'})
    CREATE (ParkerP:Person {name:'Parker Posey', born:1968})
    CREATE (DaveC:Person {name:'Dave Chappelle', born:1973})
    CREATE (SteveZ:Person {name:'Steve Zahn', born:1967})
    CREATE (TomH:Person {name:'Tom Hanks', born:1956})
    CREATE (NoraE:Person {name:'Nora Ephron', born:1941})
    CREATE
      (TomH)-[:ACTED_IN {roles:['Joe Fox']}]->(YouveGotMail),
      (MegR)-[:ACTED_IN {roles:['Kathleen Kelly']}]->(YouveGotMail),
      (GregK)-[:ACTED_IN {roles:['Frank Navasky']}]->(YouveGotMail),
      (ParkerP)-[:ACTED_IN {roles:['Patricia Eden']}]->(YouveGotMail),
      (DaveC)-[:ACTED_IN {roles:['Kevin Jackson']}]->(YouveGotMail),
      (SteveZ)-[:ACTED_IN {roles:['George Pappas']}]->(YouveGotMail),
      (NoraE)-[:DIRECTED]->(YouveGotMail)
    ```
  ![](images/neo4j_movie_create.png)
## Find 查找
  - 查找演员名 "Tom Hanks"
    ```js
    MATCH (tom {name: "Tom Hanks"}) RETURN tom
    ```
  - 查找电影名 "Cloud Atlas"
    ```js
    MATCH (cloudAtlas {title: "Cloud Atlas"}) RETURN cloudAtlas
    ```
  - 查找 10 个人
    ```js
    MATCH (people:Person) RETURN people.name LIMIT 10
    ```
  - 查找 90 年代的电影 `1990 <= released < 2000`
    ```js
    MATCH (nineties:Movie) WHERE nineties.released >= 1990 AND nineties.released < 2000 RETURN nineties.title
    ```
## Query 查询
  - 列出 "Tom Hanks" 的所有电影
    ```js
    MATCH (tom:Person {name: "Tom Hanks"})-[:ACTED_IN]->(tomHanksMovies) RETURN tom, tomHanksMovies
    ```
    ![](images/neo4j_movie_tom_hanks_movies.png)
  - "Cloud Atlas" 的导演
    ```js
    MATCH (cloudAtlas {title: "Cloud Atlas"})<-[:DIRECTED]-(directors) RETURN directors.name
    MATCH (cloudAtlas:Movie {title: "Cloud Atlas"})<-[:DIRECTED]-(director) RETURN director, cloudAtlas
    ```
    ![](images/neo4j_movie_ca_directors.png)
  - "Tom Hanks" 共同参演的演员
    ```js
    MATCH (tom:Person {name:"Tom Hanks"})-[:ACTED_IN]->(m)<-[:ACTED_IN]-(coActors) RETURN coActors.name
    MATCH (tom:Person {name: "Tom Hanks"})-[:ACTED_IN]->(m)<-[:ACTED_IN]-(coActors) RETURN tom, m, coActors
    ```
    ![](images/neo4j_movie_tom_hanks_coa.png)
  - 电影 "Cloud Atlas" 的人员关系
    ```js
    MATCH (people:Person)-[relatedTo]-(:Movie {title: "Cloud Atlas"}) RETURN people.name, Type(relatedTo), relatedTo
    MATCH (people:Person)-[relatedTo]-(ca:Movie {title: "Cloud Atlas"}) RETURN people, relatedTo, ca
    ```
    ![](images/neo4j_movie_ca_relation.png)
## Solve 最短路径 Bacon Path
  - 与 "Kevin Bacon" 4 层关系以内的演员喝电影
    ```js
    MATCH (bacon:Person {name:"Kevin Bacon"})-[*1..4]-(hollywood)
    RETURN DISTINCT hollywood

    MATCH (bacon:Person {name:"Kevin Bacon"})-[*1..2]-(hollywood)
    RETURN DISTINCT hollywood, bacon
    ```
    ![](images/neo4j_movie_becon_relation.png)
  - **Bacon path** "Kevin Bacon" 与 "Meg Ryan" 之间的最短路径
    ```js
    MATCH p=shortestPath(
      (bacon:Person {name:"Kevin Bacon"})-[*]-(meg:Person {name:"Meg Ryan"})
    )
    RETURN p
    ```
    ![](images/neo4j_movie_becon_path.png)
## Recommend 推荐
  - 为 "Tom Hanks" 推荐新的合作演员
    - 查找 Tom Hanks 合作过的演员，新的合作演员与这些演员合作过
    - Tom Hanks 没有与推荐的演员合作过
    ```js
    MATCH (tom:Person {name:"Tom Hanks"})-[:ACTED_IN]->(m)<-[:ACTED_IN]-(coActors),
          (coActors)-[:ACTED_IN]->(m2)<-[:ACTED_IN]-(cocoActors)
    WHERE NOT (tom)-[:ACTED_IN]->()<-[:ACTED_IN]-(cocoActors) AND tom <> cocoActors
    RETURN cocoActors.name AS Recommended, count(*) AS Strength ORDER BY Strength DESC
    ```
    | Recommended        | Strength |
    | ------------------ | -------- |
    | "Tom Cruise"       | 5        |
    | "Zach Grenier"     | 5        |
    | "Cuba Gooding Jr." | 4        |
    | "Carrie Fisher"    | 3        |
    | "Frank Langella"   | 2        |
    | "Ben Miles"        | 1        |
    | "Natalie Portman"  | 1        |
  - 查找能将 Tom Hanks 介绍给 Tom Cruise 的关系
    ```js
    MATCH (tom:Person {name:"Tom Hanks"})-[:ACTED_IN]->(m)<-[:ACTED_IN]-(coActors),
          (coActors)-[:ACTED_IN]->(m2)<-[:ACTED_IN]-(cruise:Person {name:"Tom Cruise"})
    RETURN tom, m, coActors, m2, cruise
    ```
    ![](images/neo4j_movie_recomm_tt.png)
## Clean up 删除电影数据
  - 关系存在的时候不能删除节点，必须同时删除节点与关系
  ```js
  // Delete all Movie and Person nodes, and their relationships
  MATCH (n) DETACH DELETE n
  // Prove that the Movie Graph is gone
  MATCH (n) RETURN n
  ```
***

# Northwind Graph
## Northwind 数据表结构
  - **RDBMS** 关系数据库管理系统 Relational Database Management System
  - **Northwind Graph** 将一个传统的关系型数据库转化为图数据库
    ```js
    :play northwind-graph
    ```
  - **Product Catalog** 产品类别，包含了 `产品 product - 类别 categories - 供应商 suppliers` 的数据

    ![](images/neo4j_nw_product_cata.png)
    - **products.csv**

      | productID | productName   | supplierID | categoryID | quantityPerUnit     | unitPrice | unitsInStock | unitsOnOrder | reorderLevel | discontinued |
      | --------- | ------------- | ---------- | ---------- | ------------------- | --------- | ------------ | ------------ | ------------ | ------------ |
      | 1         | Chai          | 1          | 1          | 10 boxes x 20 bags  | 18        | 39           | 0            | 10           | 0            |
      | 2         | Chang         | 1          | 1          | 24 - 12 oz bottles  | 19        | 17           | 40           | 25           | 0            |
      | 3         | Aniseed Syrup | 1          | 2          | 12 - 550 ml bottles | 10        | 13           | 70           | 25           | 0            |
    - **categories.csv**

      | categoryID | categoryName | description                                                | picture  |
      | ---------- | ------------ | ---------------------------------------------------------- | -------- |
      | 1          | Beverages    | Soft drinks, coffees, teas, beers, and ales                | 0x151... |
      | 2          | Condiments   | Sweet and savory sauces, relishes, spreads, and seasonings | 0x151... |
      | 3          | Confections  | Desserts, candies, and sweet breads                        | 0x151... |
    - **suppliers.csv**

      | supplierID | companyName                | contactName      | contactTitle         | address        | city        | region | postalCode | country | phone          | fax            | homePage    |
      | ---------- | -------------------------- | ---------------- | -------------------- | -------------- | ----------- | ------ | ---------- | ------- | -------------- | -------------- | ----------- |
      | 1          | Exotic Liquids             | Charlotte Cooper | Purchasing Manager   | 49 Gilbert St. | London      | NULL   | EC1 4SD    | UK      | (171) 555-2222 | NULL           | NULL        |
      | 2          | New Orleans Cajun Delights | Shelley Burke    | Order Administrator  | P.O. Box 78934 | New Orleans | LA     | 70117      | USA     | (100) 555-4822 | NULL           | #CAJUN.HTM# |
      | 3          | Grandma Kelly's Homestead  | Regina Murphy    | Sales Representative | 707 Oxford Rd. | Ann Arbor   | MI     | 48104      | USA     | (313) 555-5735 | (313) 555-3349 | NULL        |

  - **Customer Orders** 客户订单，包含了 `Custom - Order` 对应关系

    ![](images/neo4j_nw_customor_orders.png)
    - **customers.csv**

      | customerID | companyName                        | contactName    | ... |
      | ---------- | ---------------------------------- | -------------- | --- |
      | ALFKI      | Alfreds Futterkiste                | Maria Anders   | ... |
      | ANATR      | Ana Trujillo Emparedados y helados | Ana Trujillo   | ... |
      | ANTON      | Antonio Moreno Taquería            | Antonio Moreno | ... |
    - **orders.csv**

      | orderID | customerID | employeeID | orderDate               | ... |
      | ------- | ---------- | ---------- | ----------------------- | --- |
      | 10248   | VINET      | 5          | 1996-07-04 00:00:00.000 | ... |
      | 10249   | TOMSP      | 6          | 1996-07-05 00:00:00.000 | ... |
      | 10250   | HANAR      | 4          | 1996-07-08 00:00:00.000 | ... |
  - **OrderDetail** 包含了 `Order - Product` 的对应关系

    | orderID | productID | unitPrice | quantity | discount |
    | ------- | --------- | --------- | -------- | -------- |
    | 10248   | 11        | 14        | 12       | 0        |
    | 10248   | 42        | 9.8       | 10       | 0        |
    | 10248   | 72        | 34.8      | 5        | 0        |
## 加载 Product Catalog CSV 文件
  - **LOAD CSV**
    ```js
    :help cypher LOAD CSV
    ```
  - **加载 products.csv / categories.csv / suppliers.csv 文件**
    ```js
    // products.csv
    LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/products.csv" AS row
    CREATE (n:Product)
    SET n = row,
      n.unitPrice = toFloat(row.unitPrice),
      n.unitsInStock = toInteger(row.unitsInStock), n.unitsOnOrder = toInteger(row.unitsOnOrder),
      n.reorderLevel = toInteger(row.reorderLevel), n.discontinued = (row.discontinued <> "0")
    ```
    ```js
    // categories.csv
    LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/categories.csv" AS row
    CREATE (n:Category)
    SET n = row
    ```
    ```js
    // suppliers.csv
    LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/suppliers.csv" AS row
    CREATE (n:Supplier)
    SET n = row
    ```
  - **Create indexes** 创建索引
    ```js
    CREATE INDEX ON :Product(productID)
    ```
    ```js
    CREATE INDEX ON :Category(categoryID)
    ```
    ```js
    CREATE INDEX ON :Supplier(supplierID)
    ```
## 创建 Product Catalog 关系图
  - products / categories / suppliers 通过 **ID 键值** 联系起来

    ![](images/neo4j_nw_product_cata_relation.png)
  - **创建关系 relationships**
    ```js
    :help cypher MATCH
    ```
    ```js
    MATCH (p:Product),(c:Category)
    WHERE p.categoryID = c.categoryID
    CREATE (p)-[:PART_OF]->(c)
    ```
    ```js
    MATCH (p:Product),(s:Supplier)
    WHERE p.supplierID = s.supplierID
    CREATE (s)-[:SUPPLIES]->(p)
    ```
  - 查找每个供应商 supplier 提供的产品类别 categories
    ```js
    MATCH (s:Supplier)-->(:Product)-->(c:Category)
    RETURN s.companyName as Company, collect(distinct c.categoryName) as Categories
    ```
    | Company         | Categories                                    |
    | --------------- | --------------------------------------------- |
    | "Lyngbysild"    | ["Seafood"]                                   |
    | "G'day"         | ["Grains/Cereals", "Meat/Poultry", "Produce"] |
    | "Tokyo Traders" | ["Meat/Poultry", "Produce", "Seafood"]        |

  - 查找 "Produce" 类别的供应商
    ```js
    MATCH (c:Category {categoryName:"Produce"})<--(p:Product)<--(s:Supplier)
    RETURN DISTINCT s.companyName as ProduceSuppliers
    RETURN s, p, c
    ```
    ![](images/neo4j_nw_produce_relation.png)
## 加载 Customer Orders CSV 文件
  - 加载 **customers.csv** / **orders.csv** 文件
    ```js
    LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/customers.csv" AS row
    CREATE (n:Customer)
    SET n = row
    ```
    ```js
    LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/orders.csv" AS row
    CREATE (n:Order)
    SET n = row
    ```
  - **Create indexes** 创建索引
    ```js
    CREATE INDEX ON :Customer(customerID)

    CREATE INDEX ON :Order(orderID)
    ```
## 创建 Customer Orders 关系图
  ![](images/neo4j_nw_customoe_order_relation.png)
  ```js
  MATCH (c:Customer),(o:Order)
  WHERE c.customerID = o.customerID
  CREATE (c)-[:PURCHASED]->(o)
  ```
## OrderDetail CSV 文件
  - 加载 **order-details.csv** 文件，并将 **Product** 与 **Order** 关联起来
    ```js
    LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/order-details.csv" AS row
    MATCH (p:Product), (o:Order)
    WHERE p.productID = row.productID AND o.orderID = row.orderID
    CREATE (o)-[details:ORDERS]->(p)
    SET details = row,
      details.quantity = toInteger(row.quantity)
    ```
  - 综合查询
    ```js
    MATCH (cust:Customer)-[:PURCHASED]->(:Order)-[o:ORDERS]->(p:Product),
          (p)-[:PART_OF]->(c:Category {categoryName:"Produce"})
    RETURN DISTINCT cust.contactName as CustomerName, SUM(o.quantity) AS TotalProductsPurchased
    ```
***
